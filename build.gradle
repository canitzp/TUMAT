buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:3.4.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'
apply plugin: 'de.undercouch.download'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = project.tumat_version
group = "de.canitzp.tumat"
archivesBaseName = "TUMAT"

if(hasProperty('buildnumber')){
    version = "${version}-${this.properties['buildnumber']}"
}

minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
    mappings = project.forge_mappings
    replaceIn "TUMAT.java"
    replace "@VERSION@", project.version.toString()
    replace "@BUILD_DATE@", new Date().format("yyMMddHHmm")
}

repositories {
    maven { // Actually Additions
        url = "http://maven.chaosfield.at"
    }
    maven { // JEI
        url = "http://dvs1.progwml6.com"
    }
    maven { // Chisel and Bits
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven { // IC2
        url = "http://maven.ic2.player.to/"
    }
    maven { // RefinedStorage
        url = "https://repo.raoulvdberge.com/"
    }
    maven { // CommonCapabilities
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
}

dependencies {
    deobfCompile "de.ellpeck.actuallyadditions:ActuallyAdditions:${project.actadd_version}"
    deobfCompile "chameleon:Chameleon:${project.chameleon_version}"
    deobfCompile "mod.chiselsandbits:chiselsandbits:${project.chiselandbits_version}"
    deobfCompile "org.cyclops.commoncapabilities:CommonCapabilities:${project.commoncaps_version}"
    deobfCompile "extra-utilities:extrautils2:${project.extrautils2_version}"
    deobfCompile "net.sengir.forestry:forestry_${project.forestry_version}"
    deobfCompile "iChunUtil:iChunUtil:${project.ichunutil_version}"
    compileOnly "net.industrial-craft:industrialcraft-2:${project.ic2_version}:dev"
    runtime "net.industrial-craft:industrialcraft-2:${project.ic2_version}"
    deobfCompile "mezz.jei:jei_${project.jei_version}"
    deobfCompile "slimeknights.mantle:Mantle:${project.mantle_version}"
    //deobfCompile "pams-harvestcraft:Pam's+HarvestCraft:1.12.2:za"
    deobfCompile "refinedstorage:refinedstorage:${project.refinedstorage_version}"
    deobfCompile "storage-drawers:StorageDrawers:${project.storagedrawers_version}"
    deobfCompile "slimeknights:TConstruct:${project.tinkers_version}"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/canitzp/tumat/api/**'
}

javadoc {
    include 'de/canitzp/tumat/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}
