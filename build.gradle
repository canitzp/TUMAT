buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'
apply plugin: 'de.undercouch.download'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = project.tumat_version
group = "de.canizp.tumat"
archivesBaseName = "TUMAT"

if(hasProperty('buildnumber')){
    version = "${version}-${this.properties['buildnumber']}"
}

minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
    mappings = project.forge_mappings
    replaceIn "TUMAT.java"
    replace "@VERSION@", project.version.toString()
    replace "@BUILD_DATE@", new Date().format("yyMMddHHmm")
}

repositories {
    maven { // Tesla
        url = "http://maven.epoxide.xyz"
    }
    maven { // Actually Additions
        url = "http://maven.chaosfield.at"
    }
    maven { // JEI
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven { // IC2
        url = "http://maven.ic2.player.to/"
    }
    maven { // RefinedStorage
        url = "http://dl.bintray.com/raoulvdberge/dev"
    }
    maven { // MCMultiPart
        url = "http://maven.amadornes.com/"
    }
    maven { // CommonCapabilities
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
    maven { // GitHub release downloads
        url 'https://jitpack.io'
     }
}

dependencies {
    if(project.minecraft_version.toString().startsWith("1.12")){
        deobfCompile "mezz.jei:jei_" + project.minecraft_version + ":" + project.jei_version_12
        deobfCompile "de.ellpeck.actuallyadditions:ActuallyAdditions:" + project.minecraft_version + "-" + project.actadd_version_12
        compileOnly "mod.chiselsandbits:chiselsandbits:" + project.chiselandbits_version_12
        deobfCompile "refinedstorage:refinedstorage:" + project.refinedstorage_version_12 + "-+"
        deobfCompile "org.cyclops.commoncapabilities:CommonCapabilities:" + project.minecraft_version + "-" + project.commoncaps_version_12 + "-+"
        compileOnly "net.industrial-craft:industrialcraft-2:" + project.ic2_version_12 + ":dev"
        runtime "net.industrial-craft:industrialcraft-2:" + project.ic2_version_12 + ""
        deobfCompile "net.sengir.forestry:forestry_" + project.minecraft_version + ":" + project.forestry_version_12
        compileOnly "slimeknights.mantle:Mantle:" + project.mantle_version_12 + ":deobf"
        compileOnly "slimeknights:TConstruct:" + project.tinkers_version_12 + ":deobf"
        deobfCompile "com.github.jaquadro:Chameleon:cham-" + project.chamaleon_version_12
        download {
            src project.storagedrawers_url_12.toString()
            dest "./libs/Storage Drawers.jar"
        }
        download {
            src project.ichunutil_url_12.toString()
            dest "./libs/iChunUtil.jar"
        }
    }
    if(project.minecraft_version.toString().startsWith("1.11")){

    }
    compile fileTree("./libs")
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/canitzp/tumat/api/**'
}

javadoc {
    include 'de/canitzp/tumat/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}
